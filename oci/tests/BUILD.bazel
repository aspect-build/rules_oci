load("@aspect_bazel_lib//lib:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:build_test.bzl", "build_test")
load(":pull_tests.bzl", "parse_image_test")

_PLATFORM = "linux/amd64"

IMAGES_TO_TEST = {
    "distroless_java": "gcr.io/distroless/java17@sha256:161a1d97d592b3f1919801578c3a47c8e932071168a96267698f4b669c24c76d",
    "distroless_static_linux_amd64": "gcr.io/distroless/static@sha256:c3c3d0230d487c0ad3a0d87ad03ee02ea2ff0b3dcce91ca06a1019e07de05f12",
    "fluxcd_flux_single": "docker.io/fluxcd/flux:1.25.4",
    "chainguard_static": "cgr.dev/chainguard/static:latest",
}

# Use crane to pull images as a comparison for our oci_pull repository rule
[
    genrule(
        name = "pull_{}".format(repo_name),
        outs = [repo_name],
        cmd = "$(CRANE_BIN) pull {reference} $@ --format=oci --platform={platform}".format(
            reference = reference,
            platform = _PLATFORM,
        ),
        local = True,  # needs to run locally to able to use credential helpers
        message = "Pulling {reference} for {platform}".format(
            reference = reference,
            platform = _PLATFORM,
        ),
        output_to_bindir = True,
        tags = ["requires-network"],
        toolchains = [
            "@oci_crane_toolchains//:current_toolchain",
        ],
        visibility = ["//visibility:public"],
    )
    for repo_name, reference in IMAGES_TO_TEST.items()
]

[
    diff_test(
        name = "test_{}".format(repo_name),
        file1 = repo_name,
        file2 = "@{}".format(repo_name),
    )
    for repo_name, reference in IMAGES_TO_TEST.items()
]

# assert than we don't break fetching these
build_test(
    name = "pull_test",
    targets = [
        "@aws_lambda_python",
        "@debian",
        "@debian_latest",
        "@debian_stable",
        # TODO: https://github.com/bazel-contrib/rules_oci/issues/193
        # "@apollo_router",
        "@from_rules_docker",
        "@ubuntu",
    ],
)

parse_image_test(name = "parse_image_test")
