load("@aspect_bazel_lib//lib:testing.bzl", "assert_json_matches")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@container_structure_test//:defs.bzl", "container_structure_test")
load("//oci:defs.bzl", "oci_image", "oci_tarball")

# buildifier: leave-alone
oci_image(
    name = "image",
    # platform attributes
    os = "linux",
    architecture = "amd64",
    variant = "v8",
    # entrypoint and args
    entrypoint = ["/custom_bin"],
    cmd = [
        "--arg1",
        "--arg2",
    ],
    # env
    env = {
        "ENV": "/test",  # ENV=/test
        "ENV2": "/test:$ENV",  # ENV=/test:/test
        "PATH": "/usr/local/bin:$PATH",  # PATH=/usr/local/bin:
    },
    # exposed_ports
    exposed_ports = [
        "1234/tcp",
        "5678/udp",
        "5000",
    ],
    # user & workdir
    user = "root",
    workdir = "/root",
    # labels & annotations
    labels = {
        "org.opencontainers.image.version": "0.0.0",
        "org.opencontainers.image.source": "https://github.com/bazel-contrib/rules_oci=",
    },
    annotations = {
        "org.opencontainers.image.version": "0.0.0",
        "org.opencontainers.image.source": "https://github.com/bazel-contrib/rules_oci=",
    },
)

oci_image(
    name = "image_based",
    base = ":image",
    # adding a new env should preserve the previously set envs!
    env = {
        "LOCAL": "true",  # LOCAL=/test
    },
)

container_structure_test(
    name = "test",
    configs = ["test.yaml"],
    image = ":image_based",
)

repo_tags = [
    "gcr.io/empty_base:latest",
    "two:is_a_company",
    "three:is_a_crowd",  # Used to test support for more than two repo_tags.
]

# Intended to be `bazel run` to load the tarball into a container runtime.
# Produces only an mtree specification as the default output.
oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = repo_tags,
)

# Not typically recommended: ask the tarball rule to write the .tar file
# that would have been created when `bazel run`.
filegroup(
    name = "tarball.tar",
    srcs = [":tarball"],
    output_group = "tarball",
)

genrule(
    name = "tar_manifest",
    srcs = [":tarball.tar"],
    outs = ["manifest.json"],
    cmd = "tar -xOf ./$(location :tarball.tar) manifest.json > $@",
)

write_file(
    name = "expected_RepoTags",
    out = "expected_RepoTags.json",
    content = [str(repo_tags)],
)

assert_json_matches(
    name = "check_tags",
    file1 = ":tar_manifest",
    file2 = ":expected_RepoTags",
    filter1 = ".[0].RepoTags",
)
