load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//oci:defs.bzl", "oci_image")


# These numbers were gathered on a `Apple M2 Pro`
# Darwin Kernel Version 23.2.0: Wed Nov 15 21:55:06 PST 2023; root:xnu-10002.61.3~2/RELEASE_ARM64_T6020
# 10 CPU x 32GB RAM

# 1- Create an image with 10 layers 3GiB each.
# Perf: `< 50s`
N_BASE_LAYERS=10

genrule(
    name = "3gib_file",
    cmd = "head -c 3221225472 < /dev/urandom > $@",
    outs = ["3gib_file.out"],
    tags = ["manual"],
)

[
    pkg_tar(
        name = "blayer_%s" % i,
        srcs = ["3gib_file"],
        extension = "tar.gz",
        package_dir = "blayer_%s" % ("ayo" if i == 0 else i ),
        tags = ["manual"],
    )
    for i in range(N_BASE_LAYERS)
]

oci_image(
    name = "base",
    os = "linux",
    architecture = "arm64",
    tars = [":blayer_%s" % i for i in range(N_BASE_LAYERS)],
    tags = ["manual"],
)

# 2- Create an image that extends the base without adding additional layers
# Perf: `< 2s` if bazel doesn't permit tree artifact symlinks
# Perf: `< 0.5s` if bazel permits tree artifact symlinks
oci_image(
    name = "extended_wo_tars",
    base = ":base",
    tags = ["manual"],
)



# 3- Create an image that extends the base with additional layers
# Perf: `< 2s` if bazel doesn't permit tree artifact symlinks (for copying the base layers)
# Perf: `< 0.5s` if bazel permits tree artifact symlinks (for linking the base layers)
# Perf: `< 20s` for adding the new 5 layers, 3gib each.
N_LAYERS=5

[
    pkg_tar(
        name = "layer_%s" % i,
        srcs = ["3gib_file"],
        tags = ["manual"],
    )
    for i in range(N_LAYERS)
]


oci_image(
    name = "extended",
    base = ":base",
    tars = [":layer_%s" % i for i in range(N_LAYERS)],
    tags = ["manual"],
)

