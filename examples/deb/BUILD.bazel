load("//oci:defs.bzl", "oci_image", "oci_tarball")

_ARCH = [
    "amd64",
    "arm64",
]

# most container runtimes only support `zstd` or `gzip` compression
# any other compression algorithm is simply is runtime error.
# we decompress the tar archive with bsdtar and recompress with a
# known compression algorithm.
#
# TODO: this is slow as it parses the tar archive as well
# use zstd rule once we have one.
[
    genrule(
        name = "decompress_" + architecture,
        srcs = ["@bash_{}//:layer".format(architecture)],
        outs = ["_{}.tar.gz".format(architecture)],
        cmd = "$(BSDTAR_BIN) --gzip -cf $@ @$<",
        toolchains = ["@bsd_tar_toolchains//:resolved_toolchain"],
    )
    for architecture in _ARCH
]

[
    oci_image(
        name = "image_" + architecture,
        architecture = architecture,
        os = "linux",
        tars = ["_{}.tar.gz".format(architecture)],
    )
    for architecture in _ARCH
]

oci_tarball(
    name = "tarball",
    image = ":image_amd64",
    repo_tags = ["test:test"],
)
